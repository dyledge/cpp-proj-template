# Depending on the compiler, sets options for the provided interface target to enable gathering
# coverage information when the binary generated by the target (or a target that is compiled with
# the provided interface target) is run.

# Currently only support Clang-like and GCC compilers using llvm-cov and gcov respectively

# Options:
#
# ENABLE_COVERAGE default FALSE

function(enable_code_coverage project_name)

  if (CMAKE_CXX_COMPILER_ID STREQUAL "GNU" OR CMAKE_CXX_COMPILER_ID MATCHES "Clang")
    option(ENABLE_COVERAGE "Enable coverage reporting for gcc/clang" FALSE)

    if (ENABLE_COVERAGE)
      if (CMAKE_CXX_COMPILER_ID MATCHES "Clang")
        message("using llvm-cov")
        target_compile_options(${project_name} INTERFACE -fprofile-instr-generate -fcoverage-mapping)
        target_link_options(${project_name} INTERFACE -fprofile-instr-generate)
      else ()
        message("using gcov")
        target_compile_options(${project_name} INTERFACE --coverage -O0 -g)
        target_link_libraries(${project_name} INTERFACE --coverage)
      endif ()
    endif ()
  endif ()

endfunction()
